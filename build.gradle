import com.github.jengelman.gradle.plugins.processes.tasks.JavaFork

group 'tw-shokunin-aesthete'
version '1.0-SNAPSHOT'


buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }

    }
    dependencies {
        classpath('com.github.jengelman.gradle.plugins:gradle-processes:0.3.0')
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE")
        classpath 'au.com.dius:pact-jvm-provider-gradle_2.11:2.1.13'
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }

}


apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'au.com.dius.pact'
apply plugin: 'spring-boot'
apply plugin: 'com.github.johnrengelman.processes'

sourceCompatibility = 1.8
targetCompatibility = 1.8


dependencies {
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-jetty")
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile("org.springframework.boot:spring-boot-starter-test")
}

task startProvider(type: JavaFork) {
    classpath = files("build/libs/coffee-pact-${version}.jar")
    classpath += sourceSets.main.runtimeClasspath
    main = 'com.aesthete.tw.shokunin.coffeepact.Application'
    doLast {
        Thread.sleep(10000)
    }
}

task stopProvider << {
    startProvider.processHandle.abort()
}

pact {
    serviceProviders {
        coffeePactProvider {
            startProviderTask = 'startProvider'
            terminateProviderTask = 'stopProvider'
            hasPactsWith('pactoTests') {
                pactFileLocation = file('src/test/resources/contracts')
            }
        }
    }
}